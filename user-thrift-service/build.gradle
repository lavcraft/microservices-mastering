import org.gradle.internal.os.OperatingSystem

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "ru.trylogic.gradle.plugins:gradle-thrift-plugin:0.1.1"
  }
}

repositories {
  ivy {
    artifactPattern "http://dl.bintray.com/bsideup/thirdparty/[artifact]-[revision](-[classifier]).[ext]"
  }
}

apply plugin: 'thrift'

apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'com.bmuschko.docker-java-application'

task generateThriftJava(type : ru.trylogic.gradle.thrift.tasks.ThriftCompileTask) {
  generator = 'java:private-members'
  destinationDir = file("generated-src/generated/java")
}


sourceSets {
  main {
    java {
      srcDir generateThriftJava.destinationDir
    }
  }
}

clean {
  delete generateThriftJava.destinationDir
}

idea {
  module {
    sourceDirs += file('src/main/thrift')
    sourceDirs += generateThriftJava.destinationDir
  }
}

compileJava.dependsOn generateThriftJava

docker {
  javaApplication {
    baseImage = 'java'
    maintainer = 'aatarasoff'
    port = 8080
    tag = "greetings"
  }
}

dependencies {
  def thriftVersion = '0.9.2';
  Map platformMapping = [
          (OperatingSystem.WINDOWS) : 'win',
          (OperatingSystem.MAC_OS) : 'osx'
  ].withDefault { 'nix' }

  thrift "org.apache.thrift:thrift:$thriftVersion:${platformMapping.get(OperatingSystem.current())}@bin"

  compile "org.apache.thrift:libthrift:${thriftVersion}"

  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.springframework.cloud:spring-cloud-starter-eureka")

  compile 'info.developerblog.spring.thrift:spring-thrift-starter:0.0.1'
}